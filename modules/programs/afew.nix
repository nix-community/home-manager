{
  config,
  lib,
  pkgs,
  ...
}:
let
  cfg = config.programs.afew;
in
{
  options.programs.afew = {
    enable = lib.mkEnableOption "the afew initial tagging script for Notmuch";

    package = lib.mkPackageOption pkgs "afew" { nullable = true; };

    extraConfig = lib.mkOption {
      type = lib.types.lines;
      default = ''
        [SpamFilter]
        [KillThreadsFilter]
        [ListMailsFilter]
        [ArchiveSentMailsFilter]
        [InboxFilter]
      '';
      example = ''
        [SpamFilter]

        [Filter.0]
        query = from:pointyheaded@boss.com
        tags = -new;+boss
        message = Message from above

        [InboxFilter]
      '';
      description = ''
        Extra lines added to afew configuration file. Available
        configuration options are described in the afew manual:
        <https://afew.readthedocs.io/en/latest/configuration.html>.
      '';
    };
  };

  config = lib.mkIf cfg.enable {
    home.packages = lib.mkIf (cfg.package != null) [ cfg.package ];

    xdg.configFile."afew/config".text = ''
      # Generated by Home Manager.
      # See https://afew.readthedocs.io/

      ${cfg.extraConfig}
    '';
  };
}
